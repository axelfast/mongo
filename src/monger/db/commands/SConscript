# -*- mode: python -*-

Import("env")
Import("has_option")

env = env.Clone()

env.Library(
    target="test_commands_enabled",
    source=[
        "test_commands_enabled.cpp",
        env.Idlc('test_commands_enabled.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/idl/server_parameter',
        "server_status_core",
    ]
)

env.Library(
    target='server_status_core',
    source=[
        'server_status_internal.cpp',
        'server_status_metric.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/base',
    ]
)

env.Library(
    target='server_status',
    source=[
        'server_status.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/db/commands',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/db/auth/auth',
        '$BUILD_DIR/monger/db/auth/authprivilege',
        '$BUILD_DIR/monger/db/server_options_core',
        '$BUILD_DIR/monger/db/stats/counters',
        '$BUILD_DIR/monger/util/processinfo',
        '$BUILD_DIR/monger/util/net/http_client',
        'server_status_core',
    ],
)

env.Library(
    target='server_status_servers',
    source=[
        'server_status_servers.cpp',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/db/stats/counters',
        '$BUILD_DIR/monger/transport/message_compressor',
        '$BUILD_DIR/monger/transport/service_executor',
        '$BUILD_DIR/monger/util/net/ssl_manager',
        'server_status',
        'server_status_core',
    ],
)

env.Library(
    target="write_commands_common",
    source=[
        "write_commands/write_commands_common.cpp",
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/db/auth/auth',
        '$BUILD_DIR/monger/base',
        '$BUILD_DIR/monger/db/auth/authprivilege',
        '$BUILD_DIR/monger/db/commands',
        '$BUILD_DIR/monger/db/ops/write_ops_parsers',
    ],
)

env.Library(
    target="feature_compatibility_parsers",
    source=[
        "feature_compatibility_version_parser.cpp",
        "feature_compatibility_version_command_parser.cpp",
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/base',
        '$BUILD_DIR/monger/db/command_generic_argument',
        '$BUILD_DIR/monger/db/namespace_string',
    ],
)

# Commands available in every process that executes commands
env.Library(
    target='core',
    source=[
        'end_sessions_command.cpp',
        'fail_point_cmd.cpp',
        'find_and_modify_common.cpp',
        'generic.cpp',
        'hashcmd.cpp',
        'kill_all_sessions_by_pattern_command.cpp',
        'kill_all_sessions_command.cpp',
        'kill_sessions_command.cpp',
        'parameters.cpp',
        'refresh_logical_session_cache_now.cpp',
        'refresh_sessions_command.cpp',
        'rename_collection_common.cpp',
        'start_session_command.cpp',
        env.Idlc('parameters.idl')[0],
        env.Idlc('sessions_commands.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/bson/mutable/mutable_bson',
        '$BUILD_DIR/monger/db/auth/auth',
        '$BUILD_DIR/monger/db/auth/authprivilege',
        '$BUILD_DIR/monger/db/commands',
        '$BUILD_DIR/monger/db/common',
        '$BUILD_DIR/monger/db/kill_sessions',
        '$BUILD_DIR/monger/db/logical_session_cache_impl',
        '$BUILD_DIR/monger/db/logical_session_cache',
        '$BUILD_DIR/monger/db/logical_session_id_helpers',
        '$BUILD_DIR/monger/db/logical_session_id',
        '$BUILD_DIR/monger/db/mongerhasher',
        '$BUILD_DIR/monger/db/server_options_core',
        '$BUILD_DIR/monger/idl/server_parameter',
        '$BUILD_DIR/monger/logger/parse_log_component_settings',
        '$BUILD_DIR/monger/rpc/protocol',
        'test_commands_enabled',
    ],
)

# Commands available in all mongerdb server processes (mongerd, mongers, etc.)
env.Library(
    target='servers',
    source=[
        'conn_pool_stats.cpp',
        'conn_pool_sync.cpp',
        'connection_status.cpp',
        'drop_connections_command.cpp',
        'generic_servers.cpp',
        'isself.cpp',
        'logical_session_server_status_section.cpp',
        'mr_common.cpp',
        'reap_logical_session_cache_now.cpp',
        'traffic_recording_cmds.cpp',
        'user_management_commands_common.cpp',
        env.Idlc('drop_connections.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/client/clientdriver_minimal',
        '$BUILD_DIR/monger/db/commands',
        '$BUILD_DIR/monger/db/common',
        '$BUILD_DIR/monger/db/log_process_details',
        '$BUILD_DIR/monger/db/logical_session_cache_impl',
        '$BUILD_DIR/monger/db/logical_session_cache',
        '$BUILD_DIR/monger/db/logical_session_id_helpers',
        '$BUILD_DIR/monger/db/logical_session_id',
        '$BUILD_DIR/monger/db/repl/isself',
        '$BUILD_DIR/monger/db/repl/repl_coordinator_interface',
        '$BUILD_DIR/monger/db/session_catalog',
        '$BUILD_DIR/monger/db/traffic_recorder',
        '$BUILD_DIR/monger/executor/egress_tag_closer_manager',
        '$BUILD_DIR/monger/executor/task_executor_pool',
        '$BUILD_DIR/monger/rpc/client_metadata',
        '$BUILD_DIR/monger/s/coreshard',
        '$BUILD_DIR/monger/s/sharding_legacy_api',
        '$BUILD_DIR/monger/scripting/scripting_common',
        '$BUILD_DIR/monger/util/ntservice',
        'authentication_commands',
        'core',
        'feature_compatibility_parsers',
        'server_status',
        'test_commands_enabled',
    ]
)

env.Library(
    target="authentication_commands",
    source=[
        'authentication_commands.cpp',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/db/audit',
        '$BUILD_DIR/monger/db/auth/sasl_options',
        '$BUILD_DIR/monger/db/auth/user_document_parser',
        '$BUILD_DIR/monger/db/commands',
        '$BUILD_DIR/monger/util/net/ssl_manager',
    ]
)

env.Library(
    target="mongerd_fsync",
    source=[
        "fsync.cpp",
    ],
    LIBDEPS_PRIVATE=[
	'$BUILD_DIR/monger/db/auth/authprivilege',
        '$BUILD_DIR/monger/db/commands',
        '$BUILD_DIR/monger/db/concurrency/write_conflict_exception',
        '$BUILD_DIR/monger/db/curop',
        '$BUILD_DIR/monger/db/storage/backup_cursor_hooks',
        'fsync_locked',
    ]
)

env.Library(
    target="mongerd_fcv",
    source=[
        "feature_compatibility_version.cpp",
        env.Idlc('feature_compatibility_version.idl')[0],
    ],
    LIBDEPS=[
        'feature_compatibility_parsers',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/db/commands',
        '$BUILD_DIR/monger/db/dbdirectclient',
        '$BUILD_DIR/monger/db/kill_sessions_local',
        '$BUILD_DIR/monger/idl/server_parameter',
        '$BUILD_DIR/monger/executor/egress_tag_closer_manager',
    ],
)

env.Library(
    target='fsync_locked',
    source=[
        'fsync_locked.cpp',
    ],
    LIBDEPS=[
    ],
)

env.Library(
    target='list_databases_command',
    source=[
        env.Idlc('list_databases.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/base',
        '$BUILD_DIR/monger/idl/idl_parser',
    ],
)

# Commands that are present in both mongerd and embedded
env.Library(
    target="standalone",
    source=[
        "count_cmd.cpp",
        "create_indexes.cpp",
        "current_op.cpp",
        "dbcommands.cpp",
        "distinct.cpp",
        "drop_indexes.cpp",
        "explain_cmd.cpp",
        "find_and_modify.cpp",
        "find_cmd.cpp",
        "get_last_error.cpp",
        "getmore_cmd.cpp",
        "http_client.cpp",
        env.Idlc('http_client.idl')[0],
        "index_filter_commands.cpp",
        "kill_op.cpp",
        "killcursors_cmd.cpp",
        "lock_info.cpp",
        "list_collections.cpp",
        "list_databases.cpp",
        "list_indexes.cpp",
        "pipeline_command.cpp",
        "plan_cache_commands.cpp",
        "rename_collection_cmd.cpp",
        "repair_cursor.cpp",
        "run_aggregate.cpp",
        "sleep_command.cpp",
        "validate.cpp",
        "write_commands/write_commands.cpp",
        env.Idlc('create.idl')[0],
        env.Idlc('enable_coordinator_for_create_indexes_command.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/base',
        '$BUILD_DIR/monger/db/catalog/catalog_helpers',
        '$BUILD_DIR/monger/db/catalog/collection_catalog_helper',
        '$BUILD_DIR/monger/db/catalog/database_holder',
        '$BUILD_DIR/monger/db/catalog/index_key_validate',
        '$BUILD_DIR/monger/db/catalog/multi_index_block',
        '$BUILD_DIR/monger/db/command_can_run_here',
        '$BUILD_DIR/monger/db/commands',
        '$BUILD_DIR/monger/db/curop_failpoint_helpers',
        '$BUILD_DIR/monger/db/index_builds_coordinator_interface',
        '$BUILD_DIR/monger/db/ops/write_ops_exec',
        '$BUILD_DIR/monger/db/pipeline/monger_process_interface',
        '$BUILD_DIR/monger/db/query_exec',
        '$BUILD_DIR/monger/db/query/command_request_response',
        '$BUILD_DIR/monger/db/repl/replica_set_messages',
        '$BUILD_DIR/monger/db/rw_concern_d',
        '$BUILD_DIR/monger/db/stats/counters',
        '$BUILD_DIR/monger/db/stats/server_read_concern_write_concern_metrics',
        '$BUILD_DIR/monger/db/storage/storage_engine_common',
        '$BUILD_DIR/monger/db/transaction',
        '$BUILD_DIR/monger/db/views/views_mongerd',
        '$BUILD_DIR/monger/util/net/http_client',
        'core',
        'current_op_common',
        'fsync_locked',
        'kill_common',
        'list_collections_filter',
        'list_databases_command',
        'test_commands_enabled',
        'write_commands_common',
    ],
)

# Commands required by the shell to connect
env.Library(
    target="shell_protocol",
    source=[
        "test_commands.cpp",
        "whats_my_uri_cmd.cpp",
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/db/catalog/catalog_helpers',
        '$BUILD_DIR/monger/db/catalog/collection',
        '$BUILD_DIR/monger/db/commands',
        'test_commands_enabled',
    ],
)

env.Library(
    target='set_index_commit_quorum_idl',
    source=[
        env.Idlc('set_index_commit_quorum.idl')[0],
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/db/catalog/commit_quorum_options',
        '$BUILD_DIR/monger/db/rw_concern_d',
        '$BUILD_DIR/monger/idl/idl_parser',
    ],
)

# Commands that should only be present in mongerd
env.Library(
    target="mongerd",
    source=[
        "apply_ops_cmd.cpp",
        "collection_to_capped.cpp",
        "compact.cpp",
        "cpuload.cpp",
        "dbcheck.cpp",
        "dbcommands_d.cpp",
        "dbhash.cpp",
        "do_txn_cmd.cpp",
        "driverHelpers.cpp",
        "haystack.cpp",
        "mr.cpp",
        "oplog_application_checks.cpp",
        "oplog_note.cpp",
        "resize_oplog.cpp",
        "restart_catalog_command.cpp",
        "set_feature_compatibility_version_command.cpp",
        "set_index_commit_quorum_command.cpp",
        "shutdown_d.cpp",
        "snapshot_management.cpp",
        "top_command.cpp",
        "touch.cpp",
        "txn_cmds.cpp",
        "user_management_commands.cpp",
        "vote_commit_index_build_command.cpp",
        env.Idlc('vote_commit_index_build.idl')[0],
    ],
    LIBDEPS=[
        'txn_cmd_request',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/base',
        '$BUILD_DIR/monger/client/clientdriver_minimal',
        '$BUILD_DIR/monger/db/auth/auth',
        '$BUILD_DIR/monger/db/auth/authprivilege',
        '$BUILD_DIR/monger/db/auth/role_graph',
        '$BUILD_DIR/monger/db/auth/sasl_options',
        '$BUILD_DIR/monger/db/auth/user_document_parser',
        '$BUILD_DIR/monger/db/auth/user',
        '$BUILD_DIR/monger/db/background',
        '$BUILD_DIR/monger/db/catalog/catalog_control',
        '$BUILD_DIR/monger/db/catalog/catalog_helpers',
        '$BUILD_DIR/monger/db/catalog/collection_catalog_helper',
        '$BUILD_DIR/monger/db/catalog/index_key_validate',
        '$BUILD_DIR/monger/db/cloner',
        '$BUILD_DIR/monger/db/commands',
        '$BUILD_DIR/monger/db/curop_failpoint_helpers',
        '$BUILD_DIR/monger/db/dbhelpers',
        '$BUILD_DIR/monger/db/exec/stagedebug_cmd',
        '$BUILD_DIR/monger/db/index_builds_coordinator_interface',
        '$BUILD_DIR/monger/db/repl/dbcheck',
        '$BUILD_DIR/monger/db/repl/oplog',
        '$BUILD_DIR/monger/db/repl/repl_coordinator_interface',
        '$BUILD_DIR/monger/db/rw_concern_d',
        '$BUILD_DIR/monger/db/s/sharding_runtime_d',
        '$BUILD_DIR/monger/idl/idl_parser',
        '$BUILD_DIR/monger/s/sharding_legacy_api',
        '$BUILD_DIR/monger/util/net/ssl_manager',
        'core',
        'kill_common',
        'mongerd_fcv',
        'mongerd_fsync',
        'profile_common',
        'servers',
        'set_index_commit_quorum_idl',
        'shell_protocol',
        'standalone',
        'test_commands_enabled',
    ],
)

env.Library(
    target='kill_common',
    source=[
        'killcursors_common.cpp',
        'kill_op_cmd_base.cpp'
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/db/auth/authprivilege',
        '$BUILD_DIR/monger/db/audit',
        '$BUILD_DIR/monger/db/commands',
        '$BUILD_DIR/monger/db/auth/authorization_manager_global',
        '$BUILD_DIR/monger/db/query/command_request_response',
    ],
)

env.Library(
    target='current_op_common',
    source=[
        'current_op_common.cpp'
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/db/commands',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/db/namespace_string',
        '$BUILD_DIR/monger/db/pipeline/aggregation_request',
        '$BUILD_DIR/monger/db/query/command_request_response',
        '$BUILD_DIR/monger/db/service_context',
        'test_commands_enabled'
    ],
)

env.Library(
    target='profile_common',
    source=[
        'profile_common.cpp',
        env.Idlc('profile.idl')[0],
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/db/commands',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/db/auth/auth',
        '$BUILD_DIR/monger/db/auth/authprivilege',
        '$BUILD_DIR/monger/db/server_options_core',
    ],
)

env.Library(
    target="list_collections_filter",
    source=[
        'list_collections_filter.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/base',
    ],
)

env.Library(
    target='txn_cmd_request',
    source=[
        env.Idlc("txn_cmds.idl")[0],
        env.Idlc("txn_two_phase_commit_cmds.idl")[0],
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/base',
        '$BUILD_DIR/monger/idl/idl_parser',
    ]
)

if has_option('use-cpu-profiler'):
    profEnv = env.Clone()
    profEnv.InjectThirdParty('gperftools')
    profEnv.Library(
        target='cpuprofiler',
        source=[
            'cpuprofile.cpp',
        ],
        LIBDEPS=[
            '$BUILD_DIR/monger/db/commands',
            '$BUILD_DIR/monger/db/db_raii',
        ],
        PROGDEPS_DEPENDENTS=[
            '$BUILD_DIR/monger/mongerd',
        ],
    )

env.CppUnitTest(
    target="db_commands_test",
    source=[
        "index_filter_commands_test.cpp",
        "list_collections_filter_test.cpp",
        "mr_test.cpp",
        "plan_cache_commands_test.cpp",
    ],
    LIBDEPS=[
        "$BUILD_DIR/monger/db/auth/authmocks",
        "$BUILD_DIR/monger/db/commands/list_collections_filter",
        "$BUILD_DIR/monger/db/query/query_planner",
        "$BUILD_DIR/monger/db/query/query_test_service_context",
        "$BUILD_DIR/monger/db/repl/drop_pending_collection_reaper",
        "$BUILD_DIR/monger/db/repl/replmocks",
        "$BUILD_DIR/monger/db/repl/storage_interface_impl",
        "$BUILD_DIR/monger/db/service_context_d",
        "$BUILD_DIR/monger/db/service_context_d_test_fixture",
        "mongerd",
        "servers",
        "standalone",
    ],
)
