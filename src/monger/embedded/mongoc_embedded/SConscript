# -*- mode: python; -*-

import libdeps

Import("env")
Import("get_option")

env = env.Clone()

if not env['MONGO_HAVE_LIBMONGOC']:
    Return()

if get_option('install-mode') == 'hygienic':
    env.AutoInstall(
        'share/doc/mongerc_embedded',
        source=[
            '#/LICENSE-Community.txt',
            '../LICENSE-Embedded.txt',
        ],
        INSTALL_ALIAS=[
            'embedded-dev',
        ],
    )

def create_mongerc_env(env):
    mongercEnv = env.Clone()
    if mongercEnv['MONGO_HAVE_LIBMONGOC'] == "framework":
        mongercEnv.AppendUnique(FRAMEWORKS=['bson', 'mongerc'])
    else:
        mongercEnv.AppendUnique(LIBS=['bson-1.0', 'mongerc-1.0'])
    return mongercEnv

mongercEmbeddedEnv = create_mongerc_env(env)

mongercEmbeddedEnv.AppendUnique(
    CPPDEFINES=[
        'MONGOC_EMBEDDED_COMPILING',
     ],
)

if get_option('link-model') == 'static':
    mongercEmbeddedEnv.AppendUnique(
        CPPDEFINES=[
            'MONGOC_EMBEDDED_STATIC',
        ],
    )

# Please see the note in ../monger_embedded/SConscript about how to
# interpret and adjust the current and compatibility versinos.
mongercEmbeddedEnv.AppendUnique(
    SHLINKFLAGS=[
        '$MONGO_EXPORT_FILE_SHLINKFLAGS',
    ],
)

if mongercEmbeddedEnv.TargetOSIs('darwin'):
    # Please see the note in ../monger_embedded/SConscript about how to
    # interpret and adjust the current and compatibility versinos.
    mongercEmbeddedEnv.AppendUnique(
        SHLINKFLAGS=[
            '-Wl,-current_version,1',
            '-Wl,-compatibility_version,1',
        ],
    )

mongercEmbeddedTargets = mongercEmbeddedEnv.Library(
    target='mongerc_embedded',
    source=[
        'mongerc_embedded.cpp',
    ],
    LIBDEPS=[
        # No LIBDEPS or LIBDEPS_PRIVATE to monger libraries are allowed in this library. They would get duplicated in monger_embedded_capi.
        '$BUILD_DIR/monger/embedded/monger_embedded/monger_embedded',
    ],
    INSTALL_ALIAS=[
        'embedded-dev',
    ],
)

if get_option('install-mode') == 'hygienic':
    env.AutoInstall(
        'include/mongerc_embedded/v1/mongerc_embedded',
        source=['mongerc_embedded.h'],
        INSTALL_ALIAS=[
            'embedded-dev',
        ],
    )

yamlEnv = env.Clone()
yamlEnv.InjectThirdParty(libraries=['yaml'])

if get_option('link-model') != 'dynamic-sdk':
    mongercEmbeddedTestEnv = create_mongerc_env(yamlEnv)
    clientTest = mongercEmbeddedTestEnv.Program(
        target='mongerc_embedded_test',
        source=[
            'mongerc_embedded_test.cpp',
            env.Idlc('mongerc_embedded_test.idl')[0],
        ],
        LIBDEPS_PRIVATE=[
            '$BUILD_DIR/monger/base',
            '$BUILD_DIR/monger/db/server_options_core',
            '$BUILD_DIR/monger/unittest/unittest',
            '$BUILD_DIR/monger/util/options_parser/options_parser',
            'mongerc_embedded',
        ],
        INSTALL_ALIAS=[
            'embedded-test',
        ],
    )

    env.RegisterUnitTest(clientTest[0]);

# Frameworkization craziness begins here. Honestly, we should do this
# better in the future in some re-usable way, but we need to get this
# thing out the door, so here goes.

# First, we only do this in hygienic mode for the mobile targets,
# which are darwin but not macOS. For all others, we abort here. Maybe
# this should be a build flag? Since we aren't doing this for macOS,
# we can also ignore all the framework version nonsense.
if get_option('link-model') != 'dynamic-sdk' or get_option('install-mode') != 'hygienic' or not env.TargetOSIs('darwin') or env.TargetOSIs('macOS'):
    Return()

frameworkDir = env.Dir('$INSTALL_DIR/Frameworks/mongerc_embedded.framework')
env.Alias('install-embedded-dev', frameworkDir)

resourceDir = frameworkDir
if env.TargetOSIs('macOS'):
    resourceDir = resourceDir.Dir('Resources')

env.Install(
    target=resourceDir,
    source=env.File(
        name=[
            'LICENSE-Community.txt',
            'LICENSE-Embedded.txt',
        ],
        directory=env.Dir('$INSTALL_DIR/share/doc/mongerc_embedded'),
    ),
)

env.Install(
    target=frameworkDir.Dir('Headers'),
    source=env.File('mongerc_embedded.h'),
)

env.InstallAs(
    target=frameworkDir.File('Modules/module.modulemap'),
    source="mongerc_embedded.modulemap"
)

mongercEmbeddedPlist = env.Substfile(
    target="Info.plist",
    source='../Info.plist.in',
    SUBST_DICT=[
        ('@CFBundleExecutable@', 'mongerc_embedded'),
        ('@CFBundleIdentifier@', 'org.mongerdb.mongerc-embedded'),
        ('@CFBundleVersion@', env['PLIST_MONGO_BUNDLE_VERSION']),
        ('@CFBundleShortVersionString@', env['PLIST_MONGO_BUNDLE_VERSION']),
        ('@MinimumOSVersion@', env['PLIST_MINIMUM_OS_VERSION'])
    ]
)

env.Install(
    target=resourceDir,
    source=mongercEmbeddedPlist,
)

mongercEmbeddedFwLib = env.InstallAs(
    target=frameworkDir.File('mongerc_embedded'),
    source=mongercEmbeddedTargets[0],
)

env.AddPostAction(
    files=mongercEmbeddedFwLib,
    action=[
        "install_name_tool -delete_rpath @loader_path/../lib $TARGET",
        "install_name_tool -id @rpath/mongerc_embedded.framework/mongerc_embedded $TARGET",
        "install_name_tool -change @rpath/libmonger_embedded.dylib @rpath/monger_embedded.framework/monger_embedded $TARGET",
    ],
)

mongercEmbeddedDSYM = getattr(mongercEmbeddedTargets[0].attributes, "separate_debug_file", None)
if mongercEmbeddedDSYM:
    frameworkDSYMDir = '$INSTALL_DIR/Frameworks/mongerc_embedded.framework.dSYM'
    env.Alias('install-embedded-dev', frameworkDSYMDir)

    env.InstallAs(
        target=frameworkDSYMDir,
        source=mongercEmbeddedDSYM,
    )

mongercEmbeddedBCSymbolMap = getattr(mongercEmbeddedTargets[0].attributes, "bcsymbolmap_file", None)
if mongercEmbeddedBCSymbolMap:
    env.Install(
        target=frameworkDir.Dir('BCSymbolMaps'),
        source=mongercEmbeddedBCSymbolMap,
    )
