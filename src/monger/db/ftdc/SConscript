# -*- mode: python -*-
Import("env")

env = env.Clone()

ftdcEnv = env.Clone()
ftdcEnv.InjectThirdParty(libraries=['zlib'])

ftdcEnv.Library(
    target='ftdc',
    source=[
        'block_compressor.cpp',
        'collector.cpp',
        'compressor.cpp',
        'controller.cpp',
        'decompressor.cpp',
        'file_manager.cpp',
        'file_reader.cpp',
        'file_writer.cpp',
        'util.cpp',
        'varint.cpp'
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/base',
        '$BUILD_DIR/monger/bson/util/bson_extract',
        '$BUILD_DIR/monger/db/server_options_core',
        '$BUILD_DIR/monger/db/service_context',
        '$BUILD_DIR/third_party/s2/s2', # For VarInt
        '$BUILD_DIR/third_party/shim_zlib',
    ],
)

platform_libs = []

if env.TargetOSIs('linux'):
    platform_libs = [
        '$BUILD_DIR/monger/util/procparser'
    ]
elif env.TargetOSIs('windows'):
    platform_libs = [
        '$BUILD_DIR/monger/util/perfctr_collect'
    ]

env.Library(
    target='ftdc_server',
    source=[
        'ftdc_server.cpp',
        env.Idlc('ftdc_server.idl')[0],
        'ftdc_system_stats.cpp',
        'ftdc_system_stats_${TARGET_OS}.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/base',
        '$BUILD_DIR/monger/db/commands',
        '$BUILD_DIR/monger/util/processinfo',
        'ftdc'
    ] + platform_libs,
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/idl/server_parameter',
    ],
)

env.Library(
    target='ftdc_mongerd',
    source=[
        'ftdc_commands.cpp',
        'ftdc_mongerd.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/db/repl/repl_coordinator_interface',
        '$BUILD_DIR/monger/db/storage/storage_options',
        'ftdc_server'
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/db/auth/auth',
        '$BUILD_DIR/monger/db/auth/authprivilege',
    ],
)

env.Library(
    target='ftdc_mongers',
    source=[
        'ftdc_mongers.cpp',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/monger/client/clientdriver_minimal',
        '$BUILD_DIR/monger/executor/task_executor_pool',
        '$BUILD_DIR/monger/idl/server_parameter',
        '$BUILD_DIR/monger/s/grid',
        'ftdc_server',
    ],
)

env.CppUnitTest(
    target='db_ftdc_test',
    source=[
        'compressor_test.cpp',
        'controller_test.cpp',
        'file_manager_test.cpp',
        'file_writer_test.cpp',
        'ftdc_test.cpp',
        'util_test.cpp',
        'varint_test.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/db/service_context_test_fixture',
        '$BUILD_DIR/monger/util/clock_source_mock',
        'ftdc',
    ],
)
