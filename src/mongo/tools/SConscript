# -*- mode: python; -*-

Import('env')
Import('get_option')

env = env.Clone()

yamlEnv = env.Clone()
yamlEnv.InjectThirdParty(libraries=['yaml'])

mongerbridge = env.Program(
    target="mongerbridge",
    source=[
        "bridge.cpp",
        "bridge_commands.cpp",
        "mongerbridge_options.cpp",
        env.Idlc("mongerbridge_options.idl")[0],
        "mongerbridge_options_init.cpp"
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/db/dbmessage',
        '$BUILD_DIR/monger/rpc/rpc',
        '$BUILD_DIR/monger/transport/message_compressor',
        '$BUILD_DIR/monger/transport/message_compressor_options_server',
        '$BUILD_DIR/monger/transport/service_entry_point',
        '$BUILD_DIR/monger/transport/service_executor',
        '$BUILD_DIR/monger/transport/transport_layer',
        '$BUILD_DIR/monger/util/net/network',
        '$BUILD_DIR/monger/util/options_parser/options_parser_init',
        '$BUILD_DIR/monger/util/signal_handlers',
    ],
    INSTALL_ALIAS=[
        'tools'
    ],
)

mongerebench = yamlEnv.Program(
    target='mongerebench',
    source=[
        'mongerebench_main.cpp',
        'mongerebench_options.cpp',
        'mongerebench_options_init.cpp',
        env.Idlc('mongerebench_options.idl')[0],
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/db/dbdirectclient',
        '$BUILD_DIR/monger/db/storage/storage_options',
        '$BUILD_DIR/monger/db/storage/wiredtiger/storage_wiredtiger' if get_option('wiredtiger') == 'on' else [],
        '$BUILD_DIR/monger/embedded/embedded',
        '$BUILD_DIR/monger/embedded/embedded_integration_helpers',
        '$BUILD_DIR/monger/shell/benchrun',
        '$BUILD_DIR/monger/util/signal_handlers',
    ],
    INSTALL_ALIAS=[
        'embedded-test',
        'tools',
    ],
)

hygienic = get_option('install-mode') == 'hygienic'
if not hygienic:
    env.Install("#/", mongerbridge)
    env.Install("#/", mongerebench)

env.Alias('all', mongerebench)  # This ensures it compiles and links, but doesn't copy it anywhere.
