# -*- mode: python -*-
Import("env")
Import("free_monitoring")

env = env.Clone()

fmEnv = env.Clone()
fmEnv.InjectThirdParty(libraries=['snappy'])

fmEnv.Library(
    target='free_mon',
    source=[
        'free_mon_processor.cpp',
        'free_mon_queue.cpp',
        'free_mon_op_observer.cpp',
        'free_mon_storage.cpp',
        'free_mon_controller.cpp',
        env.Idlc('free_mon_protocol.idl')[0],
        env.Idlc('free_mon_commands.idl')[0],
        env.Idlc('free_mon_storage.idl')[0],
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/base',
        '$BUILD_DIR/monger/db/concurrency/lock_manager',
        '$BUILD_DIR/monger/db/dbhelpers',
        '$BUILD_DIR/monger/db/ftdc/ftdc',
        '$BUILD_DIR/monger/idl/idl_parser',
        '$BUILD_DIR/third_party/shim_snappy',
    ],
)

if free_monitoring == "on":
    fmEnv.Library(
        target='free_mon_mongerd',
        source=[
            'free_mon_commands.cpp',
            'free_mon_mongerd.cpp',
            env.Idlc('free_mon_mongerd.idl')[0],
            'free_mon_options.cpp',
            env.Idlc('free_mon_options.idl')[0],
            'free_mon_status.cpp',
        ],
        LIBDEPS=[
            'free_mon',
            '$BUILD_DIR/monger/db/commands/server_status',
            '$BUILD_DIR/monger/db/ftdc/ftdc_server',
            '$BUILD_DIR/monger/util/options_parser/options_parser',
        ],
        LIBDEPS_PRIVATE=[
            '$BUILD_DIR/monger/client/clientdriver_network',
            '$BUILD_DIR/monger/util/concurrency/thread_pool',
            '$BUILD_DIR/monger/util/net/http_client',
        ],
    )
else:
    fmEnv.Library(
        target='free_mon_mongerd',
        source=[
            'free_mon_commands_stub.cpp',
            'free_mon_stub.cpp',
        ],
        LIBDEPS_PRIVATE=[
            'free_mon',
            '$BUILD_DIR/monger/base',
            '$BUILD_DIR/monger/db/commands',
            '$BUILD_DIR/monger/db/auth/auth',
            '$BUILD_DIR/monger/db/auth/authprivilege',
        ],
    )


fmEnv.CppUnitTest(
    target='db_free_mon_test',
    source=[
        'free_mon_controller_test.cpp',
        'free_mon_queue_test.cpp',
        'free_mon_storage_test.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/monger/db/auth/authmocks',
        '$BUILD_DIR/monger/db/repl/replmocks',
        '$BUILD_DIR/monger/db/repl/storage_interface_impl',
        '$BUILD_DIR/monger/db/service_context_d_test_fixture',
        '$BUILD_DIR/monger/db/service_context_d',
        '$BUILD_DIR/monger/executor/thread_pool_task_executor_test_fixture',
        '$BUILD_DIR/monger/util/clock_source_mock',
        'free_mon',
    ],
)
